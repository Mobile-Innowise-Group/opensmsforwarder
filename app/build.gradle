buildscript {
    apply from: '../scripts/generate_apikey_properties.gradle'
    apply from: '../scripts/generate_debug_key_info.gradle'
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'io.gitlab.arturbosch.detekt' version("1.23.6")
}

android {
    namespace 'org.open.smsforwarder'
    compileSdk 34

    defaultConfig {
        applicationId "org.open.smsforwarder"
        minSdk 26
        targetSdk 34
        versionCode 3
        versionName "1.3"

        android.buildFeatures.buildConfig true
        buildConfigField "String", "OAUTH_BASE_URL", "\"https://oauth2.googleapis.com/\""
        buildConfigField "String", "API_BASE_URL", "\"https://www.googleapis.com/\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug/debug.jks')
            storePassword debugKeyProperties['DEBUG_KEYSTORE_PASSWORD']
            keyAlias debugKeyProperties['DEBUG_KEYSTORE_ALIAS']
            keyPassword debugKeyProperties['DEBUG_KEY_PASSWORD']
        }

        release {
            storeFile file("../release.jks")
            storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEYSTORE_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [
                    appName     : "OpenSmsForwarder_DEBUG",
                    appIcon     : "@mipmap/ic_launcher_debug",
                    appIconRound: "@mipmap/ic_launcher_debug_round"
            ]
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            buildConfigField "String", "CLIENT_ID", "\"${apikeyProperties['CLIENT_ID']}\""
            buildConfigField "String", "CLIENT_SECRET", "\"${apikeyProperties['CLIENT_SECRET']}\""
            buildConfigField "String", "REDIRECT_URI", "\"${apikeyProperties['REDIRECT_URI']}\""
            buildConfigField "String", "FEEDBACK_DB_PATH", "\"feedback_debug\""
        }

        release {
            manifestPlaceholders = [
                    appName     : "OpenSmsForwarder",
                    appIcon     : "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "String", "CLIENT_ID", "\"${System.getenv("CLIENT_ID")}\""
            buildConfigField "String", "CLIENT_SECRET", "\"${System.getenv("CLIENT_SECRET")}\""
            buildConfigField "String", "REDIRECT_URI", "\"${System.getenv("REDIRECT_URI")}\""
            buildConfigField "String", "FEEDBACK_DB_PATH", "\"${System.getenv("FEEDBACK_DB_PATH")}\""
        }
    }

    packaging {
        resources {
            excludes += "META-INF/DEPENDENCIES"
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

detekt {
    toolVersion = "1.23.6"
    config.from = file("config/detekt/detekt.yml")
    buildUponDefaultConfig = true
}

dependencies {
    // Common
    implementation 'androidx.core:core-ktx:1.13.1'
    // https://issuetracker.google.com/issues/280481594
    implementation 'androidx.fragment:fragment-ktx:1.8.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Splash
    implementation("androidx.core:core-splashscreen:1.0.1")

    // ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.9'

    // Navigation
    implementation 'com.github.terrakok:cicerone:7.1'

    // Retrofit
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.10.0")

    // Serialization
    implementation("com.squareup.retrofit2:converter-moshi:2.9.0")
    implementation("com.squareup.moshi:moshi-kotlin:1.14.0")

    // Room
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    ksp "androidx.room:room-compiler:2.6.1"

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.49'
    kapt 'com.google.dagger:hilt-android-compiler:2.49'

    // Hilt (for Workers)
    implementation 'androidx.hilt:hilt-work:1.2.0'
    kapt 'androidx.hilt:hilt-compiler:1.2.0'

    // WorkManager
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation "androidx.work:work-runtime-ktx:2.9.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // Gmail API
    implementation 'com.google.apis:google-api-services-gmail:v1-rev20231218-2.0.0'

    // Email sender
    implementation 'com.sun.mail:android-mail:1.6.2'
    implementation 'com.sun.mail:android-activation:1.6.2'

    // KSP
    implementation 'com.google.devtools.ksp:symbol-processing-api:1.9.22-1.0.17'

    // Google OAuth (NEW)
    implementation 'androidx.credentials:credentials:1.3.0'
    implementation 'androidx.credentials:credentials-play-services-auth:1.3.0'
    implementation 'com.google.android.libraries.identity.googleid:googleid:1.1.1'


    implementation 'com.google.android.gms:play-services-auth:21.2.0'

    // Firebase
    implementation(platform('com.google.firebase:firebase-bom:33.1.1'))
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
